#!/usr/bin/env tclsh
# -*- tcl -*-

# Arguments.
#
# repo  - fossil repository we are working from.
#
# cmd   - Command run per revision. List. Contains command and arguments.

# Results.
#
# * Runs the command per revision.
# * CWD/src - Checkout of the current revision.

package require Tcl 8.5
package require sqlite3
package require fileutil

if {[llength $argv] < 3} {
    puts stderr "Usage: $argv0 uuidfile repository cmd ..."
    exit 1
}

set cargs [lassign $argv uuidfile repo cmd]
set cmd   [file normalize $cmd]

if {![file exists $repo]} {
    puts stderr "Usage: $argv0 uuidfile repository cmd ...\nrepository $repo does not exist"
    exit 1
}

# # ## ### ##### ######## ############# ##################### ##################################

source [file dirname [file normalize [info script]]]/lib/crunch.tcl

# # ## ### ##### ######## ############# ##################### ##################################

puts "callout: $cmd R $uuidfile $cargs"

crunch repository $repo
crunch setup

foreach uuid [split [string trim [fileutil::cat $uuidfile]] \n] {
    crunch goto $uuid

    puts processing...
    set xcmd [list $cmd $repo $uuid {*}$cargs]

    if {[catch {
	exec >@ stdout 2>@ stderr {*}$xcmd
    } msg]} {
	puts "FAIL: $msg"
    }
}

crunch complete
exit 0
