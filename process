#!/usr/bin/env tclsh
# -*- tcl -*-
package require Tcl 8.5
package require sqlite3

if {[llength $argv] < 3} {
    puts stderr "Usage: $argv0 state repository cmd ..."
    exit 1
}

set cmd [lassign $argv state repo]

# state - database holding processing state for easy restart after an
#         abort. I.e. restart at point of abort, without re-doing the
#	  completed parts. database is created if not existing.

# repo  - fossil repository we are working from.

# cmd   - Command run per revision. List. Contains command and arguments.

if {![file exists $repo]} {
    puts stderr "Usage: $argv0 state repository cmd ...\nrepository does not exist"
    exit 1
}

set repo [file normalize $repo]
set base [file normalize checkout]

sqlite3 DB $repo

set has [file exists $state]
DB eval {
    ATTACH DATABASE $state AS STATE
}
if {!$has} {
    # Generate structures to hold the processing state.
    DB eval {
	-- Table of revisions we have fully processed.
	CREATE TABLE STATE.done ( uuid TEXT PRIMARY KEY );
    }
}

puts {SETUP CHECKOUT ...}
# Generate a checkout of the chosen revision under the current
# working directory.

file delete -force $base/src
file mkdir $base/src
cd $base/src
exec >@ stdout 2>@ stderr fossil open $repo
cd ../..

# Pull timeline, exclude anything we have done already, and then run
# the command on the remaining revisions, in order from past to future.

# NOTE: We do not care about branches and the like. We are _not_
# following the links between revisions. We are processing them in
# order of commit. When more than one branch is present we essentially
# run breadth-first, not depth-first.

puts {PROCESS...}

set count 0
DB eval {
    SELECT B.uuid
    FROM plink L, blob B
    WHERE B.rid = L.cid
    AND   B.uuid NOT IN STATE.done
    AND   L.isprim
    ORDER BY L.mtime DESC
} values {
    set uuid $values(uuid)
    puts "Processing : $uuid"

    cd $base/src
    exec >@ stdout 2>@ stderr fossil update $uuid
    cd ../..

    puts "Ready for  : $uuid"

    set xcmd [linsert $cmd 1 $repo $uuid $base]

    incr count
    if {![catch {
	puts ".......... : $xcmd"
	exec >@ stdout 2>@ stderr {*}$xcmd
    }]} {
	DB transaction {
	    DB eval {
		INSERT INTO STATE.done VALUES ( $uuid )
	    }
	}
    }
}

DB close

puts "Processed: $count"
exit 0
