#!/usr/bin/env tclsh8.5
# -*- tcl -*-

# Arguments
#
# repository - path to fossil repository
# uuid       - revision to build, already checked out.
# cmd + args - Command to run after the build completed.

# Side channel arguments
#
# * CWD/src - Checkout of revision to build

# Results
#
# * Runs the command per revision, after the build
# * CWD/build   - Build directory
# * CWD/install - Install directory (--prefix)
# * CWD/log     - Build log, configure & make

package require Tcl 8.5

# # ## ### ##### ######## ############# ##################### ##################################
## Argument processing

set cargs [lassign $argv repo uuid cmd]
set cmd [file normalize $cmd]

set base [file normalize [pwd]]

puts {COMPILE....}
puts "....ID......... $uuid"
puts "....SOURCES.... $base/src"
puts "....BUILD AREA. $base/build"
puts "....DESTINATION $base/install"
puts "....LOG........ $base/log"

# # ## ### ##### ######## ############# ##################### ##################################
## Path normalization to be independent of the upcoming changes to
## CWD, plus setting up derived values.

set src [file normalize $base/src]
set bld [file normalize $base/build]
set dst [file normalize $base/install]
set log [file normalize $base/log]

set configure $src/$tcl_platform(platform)/configure
set here      [pwd]

# # ## ### ##### ######## ############# ##################### ##################################

puts {....INITIALIZE}

file delete -force build install log
file mkdir         build install
#touch    log

# # ## ### ##### ######## ############# ##################### ##################################
## Building

cd build

puts {....CONFIGURE}
exec >>& $log $configure --enable-threads --prefix=$dst

puts {....MAKE}
exec >>& $log make all install

# # ## ### ##### ######## ############# ##################### ##################################
## Run the post-build code.

cd $here
set xcmd [list $cmd $repo $uuid {*}$cargs]

catch {
    puts ".......... : $xcmd"
    exec >@ stdout 2>@ stderr {*}$xcmd
}

# # ## ### ##### ######## ############# ##################### ##################################
puts {....DONE BUILD}
exit 0
